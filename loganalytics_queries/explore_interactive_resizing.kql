DatabricksClusters 
| where TimeGenerated >=todatetime("18 July 2022 11:00")
| where TimeGenerated <=todatetime("19 July 2022")
| where ActionName == 'resize' //create a cluster
| extend rparams = parse_json(RequestParams)
| extend cid = rparams.cluster_id
| extend response = parse_json(Response)
| extend StatusCode = response.statusCode
| extend ClusterId = extractjson("$.['cluster_id']", tostring(rparams))
| extend Message = response.result
| order by TimeGenerated asc
| where ClusterId =="0825-162800-bath65"

let Clusters = datatable(ClusterId:string)
    ["0825-162800-bath65"];
DatabricksClusters 
| where TimeGenerated >=todatetime("18 July 2022 11:00")
| where TimeGenerated <=todatetime("19 July 2022")
| extend rparams = parse_json(RequestParams)
| extend response = parse_json(Response)
| extend StatusCode = response.statusCode
| extend ClusterId = coalesce(rparams.cluster_id
                        ,extractjson("$.['cluster_id']", tostring(rparams))
                        ,extractjson("$.['clusterId']", tostring(rparams))
                        ,extractjson("$.['cluster_id']", tostring(response.result)))
| extend Message = response.result
| join kind=inner Clusters on ClusterId
| order by TimeGenerated asc


//just get the pool ids in use
DatabricksClusters
| where ActionName == "create"
| extend rparams = parse_json(RequestParams)
| extend response = parse_json(Response)
| extend StatusCode = response.statusCode
| extend ClusterId = coalesce(extractjson("$.['cluster_id']", tostring(rparams))
                        ,extractjson("$.['clusterId']", tostring(rparams))
                        ,extractjson("$.['cluster_id']", tostring(response.result)))
| extend ClusterName = coalesce(rparams.clusterName
                                , rparams.cluster_name)
| extend CustomTags = rparams.custom_tags, NumWorkers = rparams.num_workers
| extend CustomTagRunName = tostring(extractjson("$.['RunName']", tostring(rparams.custom_tags)))
| extend InstancePoolID = tostring(rparams.instance_pool_id)
| summarize ClusterCount=count(ClusterId) by InstancePoolID


//classify clusters using SQL endpoints vs jobs clusters
DatabricksClusters
| where ActionName == "create"
| extend rparams = parse_json(RequestParams)
| extend response = parse_json(Response)
| extend StatusCode = response.statusCode
| extend ClusterId = coalesce(extractjson("$.['cluster_id']", tostring(rparams))
                        ,extractjson("$.['clusterId']", tostring(rparams))
                        ,extractjson("$.['cluster_id']", tostring(response.result)))
| extend ClusterName = tostring(coalesce(rparams.clusterName
                                , rparams.cluster_name))
| extend CustomTags = rparams.custom_tags, NumWorkers = rparams.num_workers
| extend CustomTagRunName = tostring(extractjson("$.['RunName']", tostring(rparams.custom_tags)))
| extend InstancePoolID = tostring(rparams.instance_pool_id)
//| where isempty( InstancePoolID)
| extend NodeType = rparams.node_type_id, SQLEndpointID =extractjson("$.['SqlEndpointId']", tostring(rparams.custom_tags))
| extend ClusterType = iif(isempty(SQLEndpointID),"Job","SQL")
| summarize ClusterCount = count(ClusterId) by ClusterType, Day = bin(TimeGenerated,1d)
| render columnchart 